import org.gradle.plugins.ide.eclipse.model.Facet

apply plugin: 'java'
apply plugin: 'war'
apply plugin: 'eclipse-wtp'

compileJava.options.encoding = 'UTF-8'

sourceCompatibility = JavaVersion.VERSION_1_8   // JDK Version
targetCompatibility = JavaVersion.VERSION_1_8

webAppDirName = 'src/main/webapp'    // Webapp folder

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
        }
        resources {
            srcDir 'src/main/resouces'
        }
    }
}


// setting maven repositories address
repositories {
    mavenCentral()
    // maven { url 'http://maven.oschina.net/content/groups/public/' } // 可自訂庫的位置
}


ext {
	//version control
	springVersion = '4.1.5.RELEASE'
	jacksonVersion = '2.4.0'
	fastjsonVersion = '1.1.37'
	log4jVersion = '1.2.17'
	slf4jVersion = '1.7.2'
	springSecurityVersion = '4.0.0.RELEASE'
	springSession = '1.0.0.RELEASE'
	commonslangVersion = '3.3.2'
}

// setting dependencies
dependencies {

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0' // compiling
    providedRuntime 'javax.servlet:jstl:1.2'    // runtime
    compile 'org.springframework:spring-context:' + springVersion
    compile 'org.springframework:spring-websocket:' + springVersion
    compile 'org.springframework:spring-messaging:' + springVersion
    compile 'org.springframework:spring-webmvc:' + springVersion
    compile 'com.fasterxml.jackson.core:jackson-annotations:' + jacksonVersion
    compile 'com.fasterxml.jackson.core:jackson-databind:' + jacksonVersion
    compile 'com.fasterxml.jackson.core:jackson-core:' + jacksonVersion
    compile 'com.alibaba:fastjson:' + fastjsonVersion
    compile 'log4j:log4j:' + log4jVersion
    compile 'org.slf4j:jcl-over-slf4j:' + slf4jVersion
    compile 'org.slf4j:slf4j-log4j12:' + slf4jVersion
    compile 'org.slf4j:slf4j-api:' + slf4jVersion
    compile 'org.springframework.security:spring-security-web:' + springSecurityVersion
    compile 'org.springframework.session:spring-session:' + springSession
    compile 'org.apache.commons:commons-lang3:' + commonslangVersion
}

// setting Project Facets
eclipse {
    wtp {
        facet {
            facet name: 'jst.web', type: Facet.FacetType.fixed
            facet name: 'wst.jsdt.web', type: Facet.FacetType.fixed
            facet name: 'jst.java', type: Facet.FacetType.fixed
            facet name: 'jst.web', version: '3.1'
            facet name: 'jst.java', version: '1.8'
            facet name: 'wst.jsdt.web', version: '1.0'
        }
    }
    classpath {
		file {
			withXml {
				xml -> xml.asNode().classpathentry.findAll{
				it.@path == 'org.eclipse.jst.j2ee.internal.web.container'
			}.each {
				it.parent().remove(it)
			}
				def attrs = [kind : 'con', path : 'org.eclipse.jst.server.core.container/org.eclipse.jst.server.tomcat.runtimeTarget/Apache Tomcat v8.0']
				new Node(xml.asNode(), 'classpathentry', attrs)
			}
		}
	}
}